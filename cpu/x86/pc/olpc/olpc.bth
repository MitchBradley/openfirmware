purpose: Construct the Open Firmware module collection

command: &builder &this
in: ${BP}/cpu/x86/pc/olpc/build/ec.img
in: ${BP}/cpu/x86/pc/olpc/build/romreset.di
in: ${BP}/cpu/x86/pc/olpc/build/romstart.di
in: ${BP}/cpu/x86/pc/olpc/build/resume.di
in: ${BP}/cpu/x86/pc/olpc/build/rmstart.img
in: ${BP}/cpu/x86/pc/olpc/build/paging.di
in: ${BP}/cpu/x86/pc/olpc/build/fw.dic
in: ${BP}/dev/geode/nandflash/build/nandflash.fc
in: ${BP}/dev/mmc/sdhci/build/sdhci.fc
in: ${BP}/dev/mmc/sdhci/build/sdmmc.fc
in: ${BP}/dev/geode/ac97/build/ac97.fc
in: ${BP}/dev/usb2/hcd/ohci/build/ohci.fc
in: ${BP}/dev/usb2/hcd/ehci/build/ehci.fc
in: ${BP}/dev/usb2/device/hub/build/hub.fc
in: ${BP}/dev/usb2/device/net/build/usbnet.fc
in: ${BP}/dev/usb2/device/serial/build/usbserial.fc
in: ${BP}/dev/usb2/device/storage/build/usbstorage.fc
in: ${BP}/dev/usb2/device/keyboard/build/usbkbd.fc
in: ${BP}/dev/olpc/cafenand/build/cafenand.fc

build-now

macro: FW_MAJOR B
macro: FW_MINOR 75

fload ${BP}/cpu/x86/pc/olpc/config.fth

\ Always re-create the builton.fth file when we make a new rom.img
fload ${BP}/cpu/x86/pc/builton.bth

fload ${BP}/forth/lib/crc32.fth

hex

: pad-file  ( location -- )
   ofd @ fsize
   2dup u< abort" The ROM image is too large"
   ?do  h# ff ofd @ fputc  loop
;

.( --- Saving as olpc.rom - Direct ROM format) cr
writing olpc.rom
   " ec.img"                $add-file
   h# 2.0000 pad-file

   dropin-base rom-pa -  pad-file

   " romreset.di"           $add-file
   " romstart.di"           $add-file
   " resume.di"             $add-file

   fload ${BP}/cpu/x86/pc/olpc/loaddropins.fth

   /rom h# 400 - pad-file	\ rmstart image must start 0x400 from end
   " rmstart.img"    $add-file

\ Insert the revision signature
/rom h# 40 - ofd @ fseek
" CL1   Q2${FW_MAJOR}${FW_MINOR}  Q2${FW_MAJOR}" expand$  ofd @ fputs

/l buffer: crcbuf
/rom buffer: filebuf
0 ofd @ fseek

\ Read the entire image, compute the CRC, and store it h# 30 from the end
filebuf /rom ofd @ fgets /rom <> abort" Can't read back image"
0 crctab  filebuf /rom  ($crc)  crcbuf !

/rom h# 30 - ofd @ fseek
crcbuf /l ofd @ fputs

ofd @ fclose
