purpose: Create a Uboot-format image
\ See license at end of file

command: &builder &this
in: ${BP}/cpu/ppc/olpc/build/olpc.img
build-now

create uboot-header
  h# 27051956 be-l,
  0           be-l,  \ Header CRC, set later
  0           be-l,  \ Timestamp
  0           be-l,  \ Data size
  h# 0ff00000 be-l,  \ Load address
  h# 0ff00100 be-l,  \ Entry address
  0           be-l,  \ Data CRC, set later
  5              c,  \ OS type - Linux 
  7              c,  \ CPU type - PowerPC
  1              c,  \ Image type - standalone (kernel is 2)
  0              c,  \ Compression type - none

  here h# 20 blank
  " Open Firmware" here swap move
  h# 20 allot

here uboot-header -  constant /uboot-header

fload ${BP}/forth/lib/crc32.fth

reading olpc.img
ifd @ fsize constant /image
/image buffer: filebuf
filebuf /image ifd @ fgets  /image <>  abort" Can't read image"
ifd @ fclose

/image uboot-header 3 la+ be-l!

\ Compute the image CRC and store it in the uboot header
-1 crctab  filebuf /image  ($crc)  invert  uboot-header 6 la+ be-l!

\ Compute the header CRC and store it in the uboot header
-1 crctab  uboot-header /uboot-header  ($crc)  invert  uboot-header 1 la+ be-l!

.( --- Saving as fwuboot.rom) cr
writing fwuboot.rom
uboot-header /uboot-header ofd @ fputs
filebuf      /image        ofd @ fputs
ofd @ fclose

\ LICENSE_BEGIN
\ Copyright (c) 2007 FirmWorks
\ 
\ Permission is hereby granted, free of charge, to any person obtaining
\ a copy of this software and associated documentation files (the
\ "Software"), to deal in the Software without restriction, including
\ without limitation the rights to use, copy, modify, merge, publish,
\ distribute, sublicense, and/or sell copies of the Software, and to
\ permit persons to whom the Software is furnished to do so, subject to
\ the following conditions:
\ 
\ The above copyright notice and this permission notice shall be
\ included in all copies or substantial portions of the Software.
\ 
\ THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
\ EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
\ MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
\ NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
\ LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
\ OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
\ WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
\
\ LICENSE_END
