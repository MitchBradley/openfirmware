purpose: Build OFW Forth dictionary for OLPC XO-1.75
\ See license at end of file

dictionary: ${BP}/cpu/arm/olpc/1.75/build/prefw.dic
command: &armforth &dictionary &this
build-now

" fw.tag" r/w create-file drop  tag-file !

hex
\ ' $report-name is include-hook
\ ' noop is include-hook

fload ${BP}/cpu/arm/olpc/1.75/devices.fth

[ifndef] virtual-mode
warning off
: stand-init-io
   stand-init-io
   go-fast         \ From mmuon.fth
;
warning on
[then]

fload ${BP}/cpu/arm/linux.fth
d# 9999 to arm-linux-machine-type  \ Marvell Jasper

\ Add a tag describing the linear frame buffer
: mmp-fb-tag,  ( -- )
   8 tag-l,
   h# 54410008 tag-l, \ ATAG_VIDEOLFB
   d# 1200 tag-w,     \ Width
   d#  900 tag-w,     \ Height
   d#   24 tag-w,     \ Depth
   d# 1200 3 * tag-w, \ Pitch
   fb-pa      tag-l,  \ Base address
   d# 1200 3 *  d# 900 *  tag-l,  \ Total size - perhaps could be larger
   8     tag-b,       \ Red size
   d#  0 tag-b,       \ Red position
   8     tag-b,       \ Green size
   d#  8 tag-b,       \ Green position
   8     tag-b,       \ Blue size
   d# 16 tag-b,       \ Blue position
   0     tag-b,       \ Rsvd size
   d# 24 tag-b,       \ Rsvd position
;
' mmp-fb-tag, to fb-tag,

\ fload ${BP}/cpu/arm/mmp2/usb.fth

\ false to stand-init-debug?
true to stand-init-debug?

false value secure?

: protect-fw  ( -- )  ;

hex
: i-key-wait  ( ms -- pressed? )
   cr ." Type 'i' to interrupt stand-init sequence" cr   ( ms )
   0  do
      ukey?  if
         ukey upc ascii I  =  if  true unloop exit  then
      then
      1 ms
   loop
   false
;

warning @  warning off 
: init
\ initial-heap add-memory
   init

   standalone?  if
      disable-interrupts
      d# 1000
      i-key-wait  if
\        protect-fw
         ." Interacting" cr  hex interact
      then
      \ Turn on USB power here to overlap the time with other startup actions
      usb-power-on
   then
;
warning !
[then]

: (.firmware)  ( -- )
   ." Open Firmware  "  .built  cr
   ." Copyright 2010 FirmWorks  All Rights Reserved" cr
;
' (.firmware) to .firmware

patch merge-rect-565 merge-rect merge-cursor  \ Hack since we do all our user-level graphics in 565
patch merge-rect-565 merge-rect merge-cursor  \ Hack since we do all our user-level graphics in 565

\ Uninstall the diag menu from the general user interface vector
\ so exiting from emacs doesn't invoke the diag menu.
' quit to user-interface
fload ${BP}/cpu/arm/olpc/1.75/mfgtest.fth
[ifdef] notyet
fload ${BP}/cpu/x86/pc/olpc/via/bootmenu.fth
[then]

: screen-#lines  ( -- n )
   screen-ih 0=  if  default-#lines exit  then
   screen-ih  package( #lines )package
;
' screen-#lines to lines/page

true value text-on?
: text-off  ( -- )
   text-on?  if
      screen-ih remove-output
      false to text-on?
   then
;
: text-on   ( -- )
   text-on? 0=  if
      screen-ih add-output
      cursor-on
      true to text-on?
   then
;

fload ${BP}/cpu/x86/pc/olpc/via/banner.fth

\ This must be defined after spiui.fth, otherwise spiui will choose some wrong code
: rom-pa  ( -- adr )  mfg-data-buf mfg-data-offset -  ;  \ Fake out setwp.fth
fload ${BP}/cpu/x86/pc/olpc/setwp.fth

fload ${BP}/cpu/x86/pc/olpc/help.fth
fload ${BP}/cpu/x86/pc/olpc/gui.fth
fload ${BP}/cpu/x86/pc/olpc/strokes.fth
fload ${BP}/cpu/x86/pc/olpc/plot.fth

\ Pseudo device that appears in the boot order before net booting
0 0 " " " /" begin-package
   " prober" device-name
   : open
      visible
      false
   ;
   : close ;
end-package

fload ${BP}/dev/logdev.fth
0 value smt-test?  \ !!!
0 value final-test?  \ !!!
fload ${BP}/cpu/x86/pc/olpc/disptest.fth
fload ${BP}/dev/olpc/keyboard/selftest.fth   \ Keyboard diagnostic
fload ${BP}/dev/olpc/touchpad/touchpad.fth   \ Touchpad diagnostic
fload ${BP}/cpu/x86/pc/olpc/gridmap.fth      \ Gridded display tools
fload ${BP}/cpu/x86/pc/olpc/via/copynand.fth

: game-key@  ( -- n )  0  ;   \ Implement me!!!
fload ${BP}/cpu/x86/pc/olpc/gamekeys.fth
: emacs  ( -- )
   false to already-go?
   boot-getline to boot-file   " rom:emacs" $boot
;
defer rm-go-hook  \ Not used, but makes security happy
: tsc@  ( -- d.ticks )  timer0@ u>d  ;
d# 6500 constant ms-factor
0 value ec-indexed-io-off?    \ !!!
: ec-indexed-io-off  ( -- )  ;  \ !!!
: ec-ixio-reboot  ( -- )  ;   \ !!!
: flash-base  ( -- )  true abort" The stuff that relies on flash-base in security.fth needs to be reworked!"  ;
: cmos@  ( index -- data )
   h# 38 mod  8 +  " rtc@" clock-node @  ( index adr len ih )
   ['] $call-method catch  if  4drop 0  then
;
: cmos!  ( data index -- )
   h# 38 mod  8 +  " rtc!" clock-node @  ( data index adr len ih )
   ['] $call-method  if  2drop 3drop  then
;
: halt  ( -- )  c7-wfi  ;
fload ${BP}/cpu/x86/pc/olpc/sound.fth
fload ${BP}/cpu/x86/pc/olpc/security.fth

[ifdef] notyet
create use-thinmac
fload ${BP}/cpu/x86/bootascall.fth
fload ${BP}/cpu/x86/pc/olpc/wifichannel.fth
fload ${BP}/cpu/x86/pc/olpc/via/nbtx.fth
fload ${BP}/cpu/x86/pc/olpc/via/nbrx.fth
fload ${BP}/cpu/x86/pc/olpc/via/blockfifo.fth
[else]
0 value nb-zd-#sectors
: nandblaster  true abort" No nandblaster yet"  ;
: show-temperature  ;
[then]

fload ${BP}/cpu/x86/pc/olpc/via/fsupdate.fth
fload ${BP}/cpu/x86/pc/olpc/via/fsverify.fth
devalias fsdisk int:0

fload ${BP}/ofw/gui/ofpong.fth
[ifdef] notyet
fload ${BP}/cpu/x86/pc/olpc/life.fth
[then]

" u:\boot\olpc.fth ext:\boot\olpc.fth int:\boot\olpc.fth ext:\zimage /prober /usb/ethernet /usb/wlan"
   ' boot-device  set-config-string-default

\needs ramdisk  " " d# 128 config-string ramdisk
" "   ' boot-file      set-config-string-default   \ Let the boot script set the cmdline

3 config-int auto-boot-countdown

\ Eliminate 4 second delay in install console for the case where
\ there is no keyboard.  The delay is unnecessary because the screen
\ does not go blank when the device is closed.
patch drop ms install-console

alias reboot bye

alias crcgen drop  ( crc byte -- crc' )

\ Dictionary growth size for the ARM Image Format header
\ 1 section   before origin  section table
h# 10.0000      h# 8000 -      h# 4000 -      dictionary-size !

fload ${BP}/cpu/arm/saverom.fth  \ Save the dictionary for standalone startup

[ifdef] notyet
: c1-idle  ( -- )  interrupts-enabled?  if  halt  then  ;
' c1-idle to stdin-idle
[then]

fload ${BP}/ofw/core/countdwn.fth	\ Startup countdown

: console-start  ( -- )
   install-mux-io
   cursor-off
   true to text-on?

   " //null" open-dev to null-ih  \ For text-off state
;
: keyboard-off  ( -- )
   keyboard-ih  if
      keyboard-ih remove-input
      keyboard-ih close-dev
      0 to keyboard-ih
   then
;
: interpreter-init  ( -- )
   hex
   warning on
   only forth also definitions

   install-alarm

   page-mode
   #line off

\   .built cr
;


: factory-test?  ( -- flag )
   \ TS is the "test station" tag, whose value is set to "SHIP" at the
   \ end of manufacturing test.
   " TS" find-tag  if         ( adr len )
      ?-null  " SHIP" $=  0=  ( in-factory? )
   else                       ( )
      \ Missing TS tag is treated as not in factory test
      false
   then                       ( in-factory? )
;

[ifdef] notyet
: ?games  ( -- )
   rocker-right game-key?  if
      protect-fw
      time&date 5drop 1 and  if
         ['] pong guarded
      else
         ['] life-demo guarded
      then
      power-off
   then
;
: ?diags  ( -- )
   rocker-left game-key?  if
      protect-fw
      text-on  " test-all" ['] eval guarded
      ." Tests complete - powering off" cr  d# 5000 ms  power-off
   then
;

: ?scan-nand  ( -- )
   rocker-up game-key?  if
      protect-fw  text-on  ['] scan-nand guarded
   then
;
: ?fs-update  ( -- )
   button-check button-x or  button-o or  button-square or   ( mask )
   game-key-mask =  if  protect-fw try-fs-update  then
;
[then]


: startup  ( -- )
   standalone?  0=  if  exit  then

   block-exceptions
   no-page

   ?factory-mode

   disable-user-aborts
   console-start

[ifdef] notyet
   read-game-keys
[then]

   factory-test?  if  text-off  then

   " probe-" do-drop-in

   show-child
   install-alarm
   ['] sound catch drop
[ifdef] notyet
   ?games
[then]

   ['] false to interrupt-auto-boot?
[ifdef] probe-usb
   factory-test?  if  d# 1000 ms  then  \ Extra USB probe delay in the factory
   probe-usb
   report-disk
   report-keyboard
[then]
   " probe+" do-drop-in

   interpreter-init
[ifdef] notyet
   ?diags
[ifdef] Later
   ?scan-nand
[then]
   ?fs-update
[then]
   factory-test? 0=  if  secure-startup  then
   unblock-exceptions
   ['] (interrupt-auto-boot?) to interrupt-auto-boot?
[ifdef] notyet
   ?usb-keyboard
[then]
   auto-banner?  if  banner  then

   auto-boot

   frozen? text-on? 0=  and  ( no-banner? )
   unfreeze visible          ( no-banner? )
   if  banner  then  \ Reissue banner if it was suppressed

   blue-letters ." Type 'help' for more information." black-letters
   cr cr

   enable-user-aborts
   stop-sound   
   quit
;

: newrom
   " flash! http:\\10.20.0.14\new.rom" eval
\   " wifi media lab 802.11" eval
\   " flash http:\\18.85.46.172\new.rom" eval
;
: urom  " flash! u:\new.rom" eval  ;
: erom  " flash! ext:\new.rom" eval  ;
: no-usb-delay  " dev /usb  false to delay?  dend"  evaluate  ;
: null-fsdisk
   " dev /null : write-blocks-start 3drop ; : write-blocks-finish ; dend" evaluate
   " devalias fsdisk //null" evaluate
;

tag-file @ fclose  tag-file off

.( --- Saving fw.dic ...)
" fw.dic" $save-forth cr

fload ${BP}/cpu/arm/mmp2/rawboot.fth

.( --- Saving fw.img --- )  cr " fw.img" $save-rom

\ LICENSE_BEGIN
\ Copyright (c) 2010 FirmWorks
\ 
\ Permission is hereby granted, free of charge, to any person obtaining
\ a copy of this software and associated documentation files (the
\ "Software"), to deal in the Software without restriction, including
\ without limitation the rights to use, copy, modify, merge, publish,
\ distribute, sublicense, and/or sell copies of the Software, and to
\ permit persons to whom the Software is furnished to do so, subject to
\ the following conditions:
\ 
\ The above copyright notice and this permission notice shall be
\ included in all copies or substantial portions of the Software.
\ 
\ THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
\ EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
\ MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
\ NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
\ LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
\ OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
\ WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
\
\ LICENSE_END
