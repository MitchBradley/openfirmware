purpose: Build OFW Forth dictionary for OLPC XO-1.75
\ See license at end of file

dictionary: ${BP}/cpu/arm/olpc/1.75/build/prefw.dic
command: &armforth &dictionary &this
build-now

" fw.tag" r/w create-file drop  tag-file !

hex
\ ' $report-name is include-hook
\ ' noop is include-hook

fload ${BP}/cpu/arm/olpc/fbnums.fth
fload ${BP}/cpu/arm/olpc/fbmsg.fth

fload ${BP}/cpu/arm/olpc/1.75/devices.fth

[ifndef] virtual-mode
warning off
: stand-init-io
   stand-init-io
   go-fast         \ From mmuon.fth
;
warning on
[then]

fload ${BP}/cpu/arm/linux.fth

: usb-quiet  ( -- )
   [ ' linux-hook behavior compile, ]    \ Chain to old behavior
   unload-crypto
   " /usb" " reset-usb" execute-device-method drop
;
: quiesce  ( -- )
   usb-quiet
   close-ec
;

' quiesce to linux-hook

d# 9999 to arm-linux-machine-type  \ Marvell Jasper

\ Add a tag describing the linear frame buffer
: mmp-fb-tag,  ( -- )
   8 tag-l,
   h# 54410008 tag-l, \ ATAG_VIDEOLFB
   d# 1200 tag-w,     \ Width
   d#  900 tag-w,     \ Height
   d#   24 tag-w,     \ Depth
   d# 1200 3 * tag-w, \ Pitch
   fb-mem-va tag-l,   \ Base address
   d# 1200 3 *  d# 900 *  tag-l,  \ Total size - perhaps could be larger
   5     tag-b,       \ Red size
   d# 11 tag-b,       \ Red position
   6     tag-b,       \ Green size
   d#  5 tag-b,       \ Green position
   5     tag-b,       \ Blue size
   d#  0 tag-b,       \ Blue position
   0     tag-b,       \ Rsvd size
   d# 16 tag-b,       \ Rsvd position
;
' mmp-fb-tag, to fb-tag,

\ Add a tag describing the OFW callback
3 constant MT_DEVICE_WC
9 constant MT_MEMORY
: (ofw-tag,)  ( -- )
   4 2 * 3 +    tag-l,    \ size
   h# 41000502  tag-l,    \ ATAG_MEM
   cif-handler  tag-l,    \ Client interface handler callback address

   \ Each of these groups is a struct map_desc as defined in arch/arm/include/asm/mach/
   extra-mem-va                            tag-l,  \ VA of OFW memory
   dup  >physical pageshift rshift         tag-l,  \ Page frame number of OFW memory
   fw-mem-va /fw-mem +  extra-mem-va -     tag-l,  \ Size of OFW memory
   MT_MEMORY                               tag-l,  \ Mapping type of OFW memory

   fb-mem-va                               tag-l,  \ VA of OFW Frame Buffer
   dup >physical pageshift rshift          tag-l,  \ PA of OFW Frame Buffer
   /fb-mem                                 tag-l,  \ Size of OFW memory
   MT_DEVICE_WC                            tag-l,  \ Mapping type of OFW frame buffer
;
' (ofw-tag,) to ofw-tag,

false to stand-init-debug?
\ true to stand-init-debug?

: sec-trg   ( -- )      d# 73 gpio-set  ;  \ rising edge latches SPI_WP# low
: sec-trg?  ( -- bit )  d# 73 gpio-pin@  ;

alias ec-indexed-io-off sec-trg
alias ec-indexed-io-off? sec-trg?
alias ec-ixio-reboot ec-power-cycle  \ clears latch, brings SPI_WP# high

false value secure?

: protect-fw  ( -- )  secure?  if  flash-protect sec-trg  then  ;

hex
: i-key-wait  ( ms -- pressed? )
   cr ." Type 'i' to interrupt stand-init sequence" cr   ( ms )
   0  do
      ukey?  if
         ukey upc ascii I  =  if  true unloop exit  then
      then
      d# 1000 us  \ 1000 us is more precise than 1 ms, which is often close to 2 ms
   loop
   false
;

: rotate-button?  ( -- flag )  d# 15 gpio-pin@ 0=  ;
warning @  warning off 
: init
\ initial-heap add-memory
   init

   standalone?  if
      disable-interrupts
\     d# 1000  i-key-wait  if
      rotate-button? if
         protect-fw
         ." Interacting" cr  hex interact
      then
      \ Turn on USB power here to overlap the time with other startup actions
      usb-power-on
   then
;
warning !
[then]

: (.firmware)  ( -- )
   ." Open Firmware  "  .built  cr
   ." Copyright 2010 FirmWorks  All Rights Reserved" cr
;
' (.firmware) to .firmware

\ Uninstall the diag menu from the general user interface vector
\ so exiting from emacs doesn't invoke the diag menu.
' quit to user-interface
fload ${BP}/cpu/arm/olpc/1.75/mfgtest.fth

[ifdef] notyet
fload ${BP}/cpu/x86/pc/olpc/via/bootmenu.fth
[then]

: screen-#lines  ( -- n )
   screen-ih 0=  if  default-#lines exit  then
   screen-ih  package( #lines )package
;
' screen-#lines to lines/page

true value text-on?
: text-off  ( -- )
   text-on?  if
      screen-ih remove-output
      false to text-on?
   then
;
: text-on   ( -- )
   text-on? 0=  if
      screen-ih add-output
      cursor-on
      true to text-on?
   then
;

fload ${BP}/cpu/x86/pc/olpc/via/banner.fth

\ This must be defined after spiui.fth, otherwise spiui will choose some wrong code
: rom-pa  ( -- adr )  mfg-data-buf mfg-data-offset -  ;  \ Fake out setwp.fth
fload ${BP}/cpu/x86/pc/olpc/setwp.fth

fload ${BP}/cpu/arm/olpc/1.75/help.fth
fload ${BP}/cpu/x86/pc/olpc/gui.fth
fload ${BP}/cpu/x86/pc/olpc/strokes.fth
fload ${BP}/cpu/x86/pc/olpc/plot.fth
fload ${BP}/cpu/arm/olpc/1.75/testinstructions.fth

fload ${BP}/cpu/arm/mmp2/dramrecal.fth
fload ${BP}/cpu/arm/mmp2/rtc.fth       \ Internal RTC, used for wakeups

code halt  ( -- )  wfi   c;

fload ${BP}/cpu/arm/olpc/1.75/switches.fth \ Lid and ebook switches
fload ${BP}/cpu/arm/olpc/1.75/leds.fth     \ LEDs
fload ${BP}/cpu/x86/pc/olpc/via/factory.fth  \ Manufacturing tools

fload ${BP}/cpu/arm/olpc/1.75/accelerometer.fth
fload ${BP}/cpu/arm/olpc/1.75/compass.fth

\ Suppress long memory test at final test stage
dev /memory
defer old-diagnostic-mode?
: not-final-test?  ( -- flag )
   final-test?   if  false exit  then
   old-diagnostic-mode?
;
warning @ warning off
: selftest  ( -- error? )
   ['] (diagnostic-mode?) behavior to old-diagnostic-mode?
   ['] not-final-test?  to (diagnostic-mode?)
   selftest
   ['] old-diagnostic-mode? behavior  to (diagnostic-mode?)
;
warning !
device-end

\ When reprogramming this machine's SPI FLASH, rebooting the EC is unnecessary 
: no-kbc-reboot  ['] noop to spi-reprogrammed  ;
: kbc-on ;

\ Pseudo device that appears in the boot order before net booting
0 0 " " " /" begin-package
   " prober" device-name
   : open
      visible
      false
   ;
   : close ;
end-package

fload ${BP}/cpu/arm/mmp2/keypad.fth
[ifndef] cl2-a1
[ifdef] notdef  \ CForth turns on the keypad; resetting it makes it not work
stand-init: keypad
   keypad-on
   8 keypad-direct-mode
;
[then]
: keypad-bit  ( n keypad out-mask key-mask -- n' keypad )
   third invert  and  if    ( n keypad out-mask )
      rot or swap           ( n' keypad )
   else                     ( n keypad out-mask )
      drop                  ( n keypad )
   then                     ( n' keypad )
;
[then]

fload ${BP}/cpu/x86/pc/olpc/gamekeynames.fth

: game-key@  ( -- n )
   0                                        ( n )
[ifdef] cl2-a1
   d# 16 gpio-pin@ 0=  if  h#  80 or  then  \ O
   d# 17 gpio-pin@ 0=  if  h#  02 or  then  \ Check
   d# 18 gpio-pin@ 0=  if  h# 100 or  then  \ X
   d# 19 gpio-pin@ 0=  if  h#  01 or  then  \ Square
   d# 20 gpio-pin@ 0=  if  h#  40 or  then  \ Rotate
[else]
   d# 15 gpio-pin@ 0=  if  button-rotate or  then   ( n )
   scan-keypad                              ( n keypad )
   button-o       h# 01  keypad-bit         ( n' keypad )
   button-check   h# 02  keypad-bit         ( n' keypad )
   button-x       h# 04  keypad-bit         ( n' keypad )
   button-square  h# 08  keypad-bit         ( n' keypad )
   rocker-up      h# 10  keypad-bit         ( n' keypad )
   rocker-right   h# 20  keypad-bit         ( n' keypad )
   rocker-down    h# 40  keypad-bit         ( n' keypad )
   rocker-left    h# 80  keypad-bit         ( n' keypad )
   drop                                     ( n )
[then]
;

fload ${BP}/cpu/x86/pc/olpc/gamekeys.fth

fload ${BP}/dev/logdev.fth
fload ${BP}/cpu/x86/pc/olpc/disptest.fth
dev /ap-sp/keyboard
fload ${BP}/dev/olpc/keyboard/selftest.fth   \ Keyboard diagnostic
device-end
dev /ap-sp/mouse
fload ${BP}/dev/olpc/touchpad/syntpad.fth    \ Touchpad diagnostic
device-end
fload ${BP}/cpu/x86/pc/olpc/gridmap.fth      \ Gridded display tools
fload ${BP}/cpu/x86/pc/olpc/via/copynand.fth
\ fload ${BP}/cpu/arm/olpc/1.75/exc7200-touchscreen.fth    \ Touchscreen driver and diagnostic
fload ${BP}/cpu/arm/olpc/1.75/rm3150-touchscreen.fth    \ Touchscreen driver and diagnostic
fload ${BP}/cpu/arm/olpc/1.75/roller.fth     \ Accelerometer test

\ fload ${BP}/cpu/arm/olpc/1.75/pinch.fth  \ Touchscreen gestures
\ : pinch  " pinch" screen-ih $call-method  ;

: emacs  ( -- )
   false to already-go?
   boot-getline to boot-file   " rom:emacs" $boot
;
defer rm-go-hook  \ Not used, but makes security happy
: tsc@  ( -- d.ticks )  timer0@ u>d  ;
d# 6500 constant ms-factor

\ idt1338 rtc and ram address map
\     00 -> 0f  rtc
\     10 -> 3f  cmos

: >rtc  ( index -- rtc-address )  h# 3f and  h# 10 +  ;

: cmos@  ( index -- data )
   >rtc " rtc@" clock-node @  ( index adr len ih )
   ['] $call-method catch  if  4drop 0  then
;
: cmos!  ( data index -- )
   >rtc " rtc!" clock-node @  ( data index adr len ih )
   ['] $call-method catch  if  2drop 3drop  then
;

\ cmos address map
\     80 audio volume
\     81 audio volume
\     82 alternate boot
\     83 xid

: dimmer  ( -- )  screen-ih  if  " dimmer" screen-ih $call-method  then  ;
: brighter  ( -- )  screen-ih  if  " brighter" screen-ih $call-method  then  ;

fload ${BP}/cpu/x86/pc/olpc/sound.fth
fload ${BP}/cpu/x86/pc/olpc/guardrtc.fth
fload ${BP}/cpu/x86/pc/olpc/security.fth
2 to bundle-suffix

stand-init: xid
   h# 83 cmos@  dup 1+  h# 83 cmos!   ( n )
   d# 24 lshift               ( new-xid )
   " dev /obp-tftp  to rpc-xid  dend" evaluate
;

: pre-setup-for-linux  ( -- )
   [ ' linux-pre-hook behavior compile, ]    \ Chain to old behavior
   sound-end
;
' pre-setup-for-linux to linux-pre-hook

: show-temperature  ( -- )  space cpu-temperature .d  ;
fload ${BP}/cpu/arm/bootascall.fth
create use-thinmac
fload ${BP}/cpu/x86/pc/olpc/wifichannel.fth
fload ${BP}/cpu/x86/pc/olpc/via/nbtx.fth
fload ${BP}/cpu/x86/pc/olpc/via/nbrx.fth
fload ${BP}/cpu/x86/pc/olpc/via/blockfifo.fth

alias fast-hash crypto-hash   \ fast-hash uses acceleration when available
fload ${BP}/cpu/x86/pc/olpc/via/fsupdate.fth
fload ${BP}/cpu/x86/pc/olpc/via/fsverify.fth
devalias fsdisk int:0

\ create pong-use-touchscreen
fload ${BP}/ofw/gui/ofpong.fth
fload ${BP}/cpu/x86/pc/olpc/life.fth

" u:\boot\olpc.fth ext:\boot\olpc.fth int:\boot\olpc.fth ext:\zimage /prober /usb/ethernet /usb/wlan"
   ' boot-device  set-config-string-default

\needs ramdisk  " " d# 128 config-string ramdisk
" "   ' boot-file      set-config-string-default   \ Let the boot script set the cmdline

2 config-int auto-boot-countdown

\ Eliminate 4 second delay in install console for the case where
\ there is no keyboard.  The delay is unnecessary because the screen
\ does not go blank when the device is closed.
patch drop ms install-console

alias reboot bye

alias crcgen drop  ( crc byte -- crc' )

\ Dictionary growth size for the ARM Image Format header
\ 1 section   before origin  section table
h# 10.0000      h# 8000 -      h# 4000 -      dictionary-size !

fload ${BP}/cpu/arm/saverom.fth  \ Save the dictionary for standalone startup

fload ${BP}/ofw/core/countdwn.fth	\ Startup countdown

fload ${BP}/dev/hdaudio/noiseburst.fth  \ audio-test support package

devalias keyboard /ap-sp/keyboard
devalias mouse    /ap-sp/mouse

: console-start  ( -- )
   install-mux-io
   cursor-off
   true to text-on?

   " //null" open-dev to null-ih  \ For text-off state
;
: keyboard-off  ( -- )
   keyboard-ih  if
      keyboard-ih remove-input
      keyboard-ih close-dev
      0 to keyboard-ih
   then
;
: interpreter-init  ( -- )
   hex
   warning on
   only forth also definitions

   install-alarm

   page-mode
   #line off

\   .built cr
;


: factory-test?  ( -- flag )
   \ TS is the "test station" tag, whose value is set to "SHIP" at the
   \ end of manufacturing test.
   " TS" find-tag  if         ( adr len )
      ?-null  " SHIP" $=  0=  ( in-factory? )
   else                       ( )
      \ Missing TS tag is treated as not in factory test
      false
   then                       ( in-factory? )
;

: ?sound  ( -- )
   \ Suppress the jingle if a game key is pressed, because we don't want
   \ the jingle to interfere with diags and stuff
   -1 game-key?  if  exit  then
   ['] sound catch drop
;

: ?games  ( -- )
   rocker-right game-key?  if
      protect-fw
      time&date 5drop 1 and  if
         ['] pong guarded
      else
         ['] life-demo guarded
      then
      power-off
   then
;
: ?diags  ( -- )
   rocker-left game-key?  if
      protect-fw
      text-on  " test-all" ['] eval guarded
      ." Tests complete - powering off" cr  d# 5000 ms  power-off
   then
;

: ?fs-update  ( -- )
   button-check button-x or  button-o or  button-square or   ( mask )
   game-key-mask =  if  protect-fw try-fs-update  then
;

fload ${BP}/cpu/arm/mmp2/clocks.fth

: startup  ( -- )
   standalone?  0=  if  exit  then

   block-exceptions
   no-page

   ?factory-mode

   disable-user-aborts
   console-start

   read-game-keys

   factory-test? 0=  if  text-off  then

   " probe-" do-drop-in

   show-child

   update-ec-flash?  if  show-reflash  update-ec-flash  then

   install-alarm
   ?sound

   ?games

   ['] false to interrupt-auto-boot?
[ifdef] probe-usb
   factory-test?  if  d# 1000 ms  then  \ Extra USB probe delay in the factory
   probe-usb
   report-disk
   report-keyboard
[then]
   " probe+" do-drop-in

   interpreter-init

   ?diags
   ?fs-update

   factory-test? 0=  if  secure-startup  then
   unblock-exceptions
   ['] (interrupt-auto-boot?) to interrupt-auto-boot?

   ?usb-keyboard

   auto-banner?  if  banner  then

   auto-boot

   frozen? text-on? 0=  and  ( no-banner? )
   unfreeze visible cursor-on ( no-banner? )
   if  banner  then  \ Reissue banner if it was suppressed

   blue-letters ." Type 'help' for more information." black-letters cancel
   cr cr

   enable-user-aborts
   stop-sound   
   quit
;

: newrom
   " flash! http:\\192.168.200.200\new.rom" eval
;
: newec
   " flash-ec http:\\192.168.200.200\ecimage.bin" eval
;
: qz
   " qz" $essid  " http:\\qz\" included  \ qa test bed scripting, james cameron
;
: urom  " flash! u:\new.rom" eval  ;
: uec   " flash-ec! u:\ecimage.bin" eval  ;
: erom  " flash! ext:\new.rom" eval  ;
: no-usb-delay  " dev /usb  false to delay?  dend"  evaluate  ;
: null-fsdisk
   " dev /null : write-blocks-start 3drop ; : write-blocks-finish ; dend" evaluate
   " devalias fsdisk //null" evaluate
;

tag-file @ fclose  tag-file off

.( --- Saving fw.dic ...)
" fw.dic" $save-forth cr

fload ${BP}/cpu/arm/mmp2/rawboot.fth

.( --- Saving fw.img --- )  cr " fw.img" $save-rom

\ LICENSE_BEGIN
\ Copyright (c) 2010 FirmWorks
\ 
\ Permission is hereby granted, free of charge, to any person obtaining
\ a copy of this software and associated documentation files (the
\ "Software"), to deal in the Software without restriction, including
\ without limitation the rights to use, copy, modify, merge, publish,
\ distribute, sublicense, and/or sell copies of the Software, and to
\ permit persons to whom the Software is furnished to do so, subject to
\ the following conditions:
\ 
\ The above copyright notice and this permission notice shall be
\ included in all copies or substantial portions of the Software.
\ 
\ THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
\ EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
\ MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
\ NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
\ LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
\ OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
\ WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
\
\ LICENSE_END
